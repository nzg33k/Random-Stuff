HAI 1.2
  OBTW 
    This is my first LOLCode program
    This language amuses me and yet sounds usable :)
    I know there are better ways to do things and I know it's a bit pointless, but it's fun :)

    If the program is passed a NUMBR it will tell you if it is even or odd
    If you pass it a YARN (or anything that is not a NUMBR) it will greet you appropriately
    NUMBARs are treated as YARNS, not NUMBRS
  TLDR


  BTW Let's create a BUKKIT and (creatively) name it bucket
  O HAI IM bucket

    I HAS A comment ITZ "W3lc0m3 t0 mi pr0gr4m3!:)If u g0tt4 g0, sai bai:)"
    BTW Respond to YARNS
    HOW IZ I sayhai YR words 
      I HAS A rply ITZ A YARN
      BOTH SAEM words AN "hai", O RLY?
        YA RLY
          rply R "Hai!"
        MEBBE BOTH SAEM words AN "hi"
          rply R "U dnt spk LOL"
        NO WAI
          rply R SMOOSH "Wachu talkin bout?  '" AN words AN "' iz l4m3" MKAY
        OIC
      FOUND YR rply
    IF U SAY SO

    BTW Check if a NUMBR is even or not
    HOW IZ I even YR num
      num R MAEK num A NUMBR
      I HAS A rply ITZ A TROOF
      I HAS A res ITZ MOD OF num AN 2
      I HAS A txtbck ITZ A YARN
      BOTH SAEM res AN 0, O RLY?
        YA RLY
          txtbck R "Even"
        NO WAI
          txtbck R "Odd"
      OIC
      FOUND YR txtbck
    IF U SAY SO

    HOW IZ I numbr YR num
      I HAS A rply ITZ A TROOF
      I HAS A numbar ITZ A NUMBAR
      I HAS A numbr ITZ A NUMBR
      numbar R MAEK num NUMBAR
      numbr R MAEK num NUMBR

      BTW YARNS turn to 0 when cast to NUMBRs unless they are actually NUMB[A]Rs
      BOTH SAEM numbr AN 0, O RLY?
        YA RLY
          rply R FAIL
        NO WAI
          BTW Check if it's a NUMBAR, we don't handle NUMBARs
          BTW This approach will only account for the first 5dp, good enough for this though :) 
          numbar R PRODUKT OF numbar AN 10 
          numbar R QUOSHUNT OF numbar AN numbr
          DIFFRINT 10 AN numbar, O RLY?
            YA RLY
              rply R FAIL
            NO WAI
              rply R WIN
          OIC
      OIC
      FOUND YR rply
    IF U SAY SO
  KTHX

  CAN HAS STDIO?

  I HAS A txtspls ITZ A YARN
  BTW Instantiate bucket as pail
  I HAS A pail ITZ LIEK A bucket
  I HAS A numbrchk ITZ A TROOF
  I HAS A counter ITZ 0
  I HAS A bai ITZ FAIL
  VISIBLE pail'Z comment
  IM IN YR loop UPPIN YR counter TIL bai
    VISIBLE ":)Wot u sai?"
    GIMMEH txtspls
    numbrchk R pail IZ numbr YR txtspls MKAY

    numbrchk, O RLY?
      YA RLY
        VISIBLE pail IZ even YR txtspls MKAY
      NO WAI
        txtspls, WTF?
          OMG "WIN"
            VISIBLE "TROOF WIN!"
            GTFO
          OMG "FAIL"
            VISIBLE "TROOF FALSE!"
            GTFO
          OMG "bai"
            VISIBLE SMOOSH counter AN " commands run!  Bai!" MKAY
            bai R WIN
            GTFO
          OMGWTF
            VISIBLE pail IZ sayhai YR txtspls MKAY
            GTFO
      OIC
    OIC
  IM OUTTA YR loop
KTHXBYE
